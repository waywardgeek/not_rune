goal:
| topStatements
| imports topStatements
;

imports: "(" "import" paths ")" -> ("import" $3)
;

paths: -> ()
| paths path -> $1.$2
;

path: IDENT -> $1:ref
| dottedPath
;

dottedPath: "(" idents ")" -> $2
;

idents: IDENT -> ($1:ref)
| idents IDENT -> $1.$2:dot
;

topStatements: statement NEWLINE -> ($1)
| topStatements statement NEWLINE -> $1.$2
;

statement: executableStatement
| declaration
;

statementList: "(" statements ")" -> $2
;

statements: -> ()
| statements statement -> $1.$2
;

executableStatement: assignStatement
| switchStatement
| callStatement
| returnStatement
| deleteStatement
| ifStatement
| whileStatement
;

assignStatement: "(" "=" accessExpr expr ")" -> ("=" $3 $4)
;

ifStatement: "(" "if" boolExpr statementList optStatementList ")"
    -> ("if" $3 $4 $5)
;

whileStatement: "(" "while" boolExpr optStatementList ")"
    -> ("while" $3 $4)
| "(" "do" statementList "while" boolExpr optStatementList ")"
    -> ("do" $3 "while" $5 $6)
;

optStatementList:
| statementList
;

callStatement: functionCall
;

returnStatement: "(" "return" optExpr ")" -> ("return" $3)
;

optExpr:
| expr
;

deleteStatement: "(" "delete" accessExpr ")" -> ("delete" $3)
;

switchStatement: "(" "switch" arithExpr cases optDefaultCase ")" -> ("switch" $3).$4.$5
;

cases: case -> ($1)
| cases case -> $1.$2
;

case: "(" constArithExpr statementList ")" -> ($2 $3)
;

optDefaultCase:
| "(" "default" statementList ")"
;

declaration: varDecl
| functionDecl
| classDecl
;

varDecl: "(" "var" IDENT optExpr ")" -> ("var" $3:def $4)
;

functionDecl: "(" "func" type IDENT "(" functionParamDecls ")" statementList ")"
    -> ("func" $3 $4:scoped $6 $8)
| "(" "func" IDENT "(" functionParamDecls ")" statementList ")"
    -> ("func" null $3:scoped $5 $7)
;

functionParamDecls: -> ()
| functionParamDecls type IDENT -> $1.$2.$3:def
;

classDecl: "(" "class" IDENT classDeclarations ")" -> ("class" $3:scoped).$4
;

classDeclarations: -> ()
| classDeclarations declaration -> $1.$2
;

functionCall: "(" accessExpr exprs ")" -> ($2)+$3
;

exprs: -> ()
| exprs expr -> $1.$2
;

type: path
| "(" "array" type optSize ")" -> ("array" $3 $4)
;

optSize:
| INTEGER
;

expr: boolOpExpr
| arithOpExpr
| accessExpr
| functionCall
| newExpr
| INTEGER
| FLOAT
;

accessExpr: IDENT -> $1:ref
| "(" "index" accessExpr arithExpr ")" -> ("index" $3 $4)
| "(" "dot" accessExpr IDENT ")" -> ("dot" $3 $4:dot)
;

boolExprs: -> ()
| boolExprs boolExpr -> $1.$2
;

boolExpr: boolOpExpr
| accessExpr
| functionCall
;

boolOpExpr: logicalExpr
| relationalExpr
;

logicalExpr: "(" "||" boolExprs ")" -> ("||").$3
| "(" "&&" boolExprs ")" -> ("&&").$3
| "(" "!" boolExpr ")" -> ("!" $3)
;

relationalExpr: "(" "==" expr expr ")" -> ("==" $3 $4)
| "(" "!=" expr expr ")" -> ("!=" $3 $4)
| "(" relationalOp arithExpr arithExpr ")" -> ($2 $3 $4)
;

relationalOp: ">"
| "<"
| ">="
| "<="
;

arithExpr: arithOpExpr
| accessExpr
| functionCall
| INTEGER
| FLOAT
;

arithOpExpr: "(" multiArithOp arithExprs ")" -> ($2)+$3
| "(" binaryArithOp arithExpr arithExpr ")" -> ($2 $3 $4)
| "(" unaryArithOp arithExpr ")" -> ($2 $3)
| "(" "-" arithExpr arithExpr ")" -> ("-" $3 $4)
| "(" "-" arithExpr ")" -> ("-" $3)
;

arithExprs: -> ()
| arithExprs arithExpr -> $1.$2
;

multiArithOp: "+"
| "*"
| "&"
| "|"
| "^"
;

binaryArithOp: "/"
| "%"
| "<<"
| ">>"
;

unaryArithOp: "~"
;

constArithExpr: path
| INTEGER
;

newExpr: "(" "new" path ")" -> ("new" $3)
;
