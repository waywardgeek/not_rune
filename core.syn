goal:
| topStatements
| imports topStatements
;

imports: "(" "import" paths ")" -> ("import" $3)
;

paths: -> ()
| paths path -> $1.$2
;

path: IDREF -> $1
| "(" "." path IDREF ")" -> ("." $3 $4)
;

topStatements: statement NEWLINE -> ($1)
| topStatements statement NEWLINE -> $1.$2
;

statement: executableStatement
| declaration
;

statementList: "(" statements ")" -> $2
;

statements: -> ()
| statements statement -> $1.$2
;

executableStatement: assignStatement
| switchStatement
| callStatement
| returnStatement
| deleteStatement
| ifStatement
| whileStatement
;

assignStatement: "(" "=" accessExpr expr ")" -> ("=" $3 $4)
;

ifStatement: "(" "if" boolExpr statementList optStatementList ")"
    -> ("if" $3 $4 $5)
;

whileStatement: "(" "while" boolExpr optStatementList ")"
    -> ("while" $3 $4)
| "(" "do" statementList "while" boolExpr optStatementList ")"
    -> ("do" $3 "while" $5 $6)
;

optStatementList:
| statementList
;

callStatement: functionCall
;

returnStatement: "(" "return" optExpr ")" -> ("return" $3)
;

optExpr:
| expr
;

deleteStatement: "(" "delete" accessExpr ")" -> ("delete" $3)
;

switchStatement: "(" "switch" arithExpr cases optDefaultCase ")" -> ("switch" $3).$4.$5
;

cases: case -> ($1)
| cases case -> $1.$2
;

case: "(" constArithExpr statementList ")" -> ($2 $3)
;

optDefaultCase:
| "(" "default" statementList ")"
;

declaration: varDecl
| functionDecl
| classDecl
;

varDecl: "(" "var" IDVAR optExpr ")" -> ("var" $3 $4)
;

functionDecl: "(" "func" type IDFUNC "(" functionParamDecls ")" statementList ")"
    -> ("func" $3 $4 $6 $8)
| "(" "func" IDFUNC "(" functionParamDecls ")" statementList ")"
    -> ("func" $3 $5 $7)
;

functionParamDecls: -> ()
| functionParamDecls type IDVAR -> $1.$2.$3
;

classDecl: "(" "class" IDTYPE classDeclarations ")" -> ("class" $3).$4
;

classDeclarations: -> ()
| classDeclarations declaration -> $1.$2
;

functionCall: "(" "call" accessExpr exprs ")" -> ($3)+$4
;

exprs: -> ()
| exprs expr -> $1.$2
;

type: path
| "(" "array" path constArithExpr ")" -> ("array" $3 $4)
| "(" "array" path ")" -> ("array" $3)
;

expr: boolOpExpr
| arithOpExpr
| accessExpr
| functionCall
| newExpr
| INTEGER
| FLOAT
;

accessExpr: IDREF -> $1
| "(" "index" accessExpr arithExpr ")" -> ("index" $3 $4)
| "(" "." accessExpr IDREF ")" -> ("." $3 $4)
;

boolExprs: -> ()
| boolExprs boolExpr -> $1.$2
;

boolExpr: boolOpExpr
| accessExpr
| functionCall
;

boolOpExpr: logicalExpr
| relationalExpr
;

logicalExpr: "(" "||" boolExprs ")" -> ("||").$3
| "(" "&&" boolExprs ")" -> ("&&").$3
| "(" "!" boolExpr ")" -> ("!" $3)
;

relationalExpr: "(" "==" expr expr ")" -> ("==" $3 $4)
| "(" "!=" expr expr ")" -> ("!=" $3 $4)
| "(" relationalOp arithExpr arithExpr ")" -> ($2 $3 $4)
;

relationalOp: ">"
| "<"
| ">="
| "<="
;

arithExpr: arithOpExpr
| accessExpr
| functionCall
| INTEGER
| FLOAT
;

arithOpExpr: "(" multiArithOp arithExprs ")" -> ($2)+$3
| "(" binaryArithOp arithExpr arithExpr ")" -> ($2 $3 $4)
| "(" unaryArithOp arithExpr ")" -> ($2 $3)
| "(" "-" arithExpr arithExpr ")" -> ("-" $3 $4)
| "(" "-" arithExpr ")" -> ("-" $3)
;

arithExprs: -> ()
| arithExprs arithExpr -> $1.$2
;

multiArithOp: "+"
| "*"
| "&"
| "|"
| "^"
;

binaryArithOp: "/"
| "%"
| "<<"
| ">>"
;

unaryArithOp: "~"
;

constArithExpr: path
| INTEGER
;

newExpr: "(" "new" path ")" -> ("new" $3)
;
